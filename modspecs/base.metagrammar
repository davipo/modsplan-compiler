# base.metagrammar
# Base syntax for Modsplan grammars

# This is also the metagrammar for itself!
# Space character may appear only where noted; extra spaces ignored


grammar => preface body

preface => preface_line*

preface_line => comment "\n"    # "\n" represents a newline (end-of-line marker)
preface_line => import "\n"

import => 'use ' filename,+     # one or more filenames, separated by ", "

filename => identifier      # no extension, assume same extension as current file

body => body_line*

body_line => production? comment? "\n"

comment => "#" P*           # if not at start of line, must be preceded by a space

production => nonterminal " => " alternate

nonterminal => nonterm      # "nonterminal" is left side only; "nonterm" may be on right

nonterm => identifier

identifier => L+

alternate => word+          # spaces must separate adjacent words

word => item repeater?

repeater => separator? quantifier

separator => ","            # separates repeated items (space must follow each)

quantifier => "*"           # zero or more
quantifier => "+"           # one or more
quantifier => "?"           # zero or one

item => nonterm 
item => literal             # terminal

literal => "'" P* "'"
literal => '"' P* '"'

# charclass is permitted only in .tokens spec; here only because used in this metagrammar
item => charclass           # terminal

charclass => U

# Character classes are represented by a single uppercase letter:
# U => any upper case letter
# L => any lower case letter
# D => any digit
#
# P* = any printable characters, must be followed by terminal or end of line.
# P may be used only with *
# Printable char may be a space; tab char discouraged

