# L0.syntax
# L0 language syntax
# (A minimal subset of Python)

use expr        # common expression syntax


module.root => declaration+

declaration => NEWLINE
declaration => globalvar NEWLINE
declaration => extfunc NEWLINE
declaration => funcdef

globalvar => 'var' TYPE identifier
globalvar => 'var' TYPE identifier '=' constant

extfunc => 'external' TYPE identifier '('  ')'      # external function


funcdef => 'def' VOID identifier params ':' NEWLINE suite
funcdef => 'def' TYPE identifier params ':' NEWLINE suite

params => '(' identifier,* ')'

suite => INDENT statement+ DEDENT

statement => NEWLINE
statement => vardecl
statement => if
statement => while
statement => return NEWLINE
statement => call NEWLINE
statement => assignment NEWLINE

vardecl => 'var' TYPE identifier 
vardecl => 'var' TYPE identifier '=' constant

if => 'if' test ':' NEWLINE suite else? 
else => 'else' ':' NEWLINE suite

while => 'while' test ':' NEWLINE suite

return => 'return' test?    # return value must match return TYPE;
                            #   no value iff TYPE is void.

assignment => designator '=' test
assignment => designator ASSIGN_OP test

designator => variable

test => NOT_OP comparison
