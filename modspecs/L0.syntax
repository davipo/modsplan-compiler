# L0.syntax
# L0 language syntax
# (A minimal subset of Python)

use expr        # common expression syntax


module.root => line+

line => NEWLINE
line => statement


statement => extdecl NEWLINE
statement => extfunc NEWLINE
statement => vardecl NEWLINE
statement => if
statement => while
statement => funcdef
statement => call NEWLINE
statement => assignment NEWLINE

extdecl => 'external' TYPE identifier

extfunc => 'external' TYPE identifier '('  ')'      # external function

vardecl => 'var' TYPE identifier 


if => 'if' test ':' NEWLINE suite else? 
else => 'else' ':' NEWLINE suite

suite => INDENT line+ DEDENT


while => 'while' test ':' NEWLINE suite


funcdef => 'def' TYPE identifier params ':' NEWLINE suite
params => '(' identifier,* ')'


assignment => designator ASSIGN test

designator => variable

