# c1.syntax
# c1 language syntax
# A working subset of C

use expr        ## common expression syntax


translationunit.root => extdecl+

extdecl => globalvardeclaration ';'
extdecl => funcdeclaration ';'
extdecl => funcdef


globalvardeclaration => typespecifier initdeclarator,+


typespecifier => TYPESPEC
## typespecifier => structspecifier
## typespecifier => typedefname


initdeclarator => declarator '=' constantexpr
initdeclarator => declarator


constantexpr => conditionalexpr_constant
    ## use subtype _constant to restrict conditionalexpr?
conditionalexpr_constant => test    # test defined in expr.syntax


declarator => pointr* directdeclarator

pointr => '*' TYPEQUALIFIER*

directdeclarator => identifier followdeclarator*

followdeclarator => arraydeclarator
followdeclarator => functiondeclarator
## followdeclarator => funcdeclarator

arraydeclarator => '[' expression ']'
arraydeclarator => '[' '*'? ']'

## funcdeclarator => '(' paramdecl,* ellipsis? ')'
## paramdecl => declspecifier+ abstractdeclarator
    # Need abstractdeclarator for parameters without name in function declarations


funcdeclaration => typespecifier identifier functiondeclarator

funcdef => typespecifier identifier functiondeclarator compoundstatement

functiondeclarator => '(' paramdeclaration,* ellipsis? ')'
    ##  This requires parameters in a function declaration to include their names,
    ##      as well as their types.

paramdeclaration => declspecifier+ declarator

ellipsis => ','  '...'


compoundstatement => '{' blockitem* '}'
blockitem => declaration
blockitem => statement

statement => if
statement => while
statement => for
statement => compoundstatement
statement => expression? ';'

if => 'if' '(' test ')' suite else?
else => 'else' suite

while => 'while' '(' test ')' suite

for => 'for' '(' expression? ';' expression? ';' expression? ')' statement
for => 'for' '(' declaration expression? ';' expression? ')' statement

suite => statement      # for compatibility with base.defn

expression => assignexpr
expression => test      # test specified in expr.syntax

assignexpr => lvalue ASSIGN expression

lvalue => variable

atom => INC_OP variable 
atom => variable INC_OP

