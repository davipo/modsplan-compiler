# c1.syntax
# c1 language syntax
# A working subset of C

use constants   # constant, number
use expr        ## common expression syntax


translationunit.root => extdeclaration+

extdeclaration => typedef ';'
extdeclaration => globalvardeclaration ';'
extdeclaration => funcdeclaration ';'
extdeclaration => funcdefinition


typedef => 'typedef' typespecifier declarator

globalvardeclaration => declspecifier initdeclarator,+

declspecifier => STORAGESPEC? TYPEQUALIFIER* typespecifier

initdeclarator => identifier initializer?
## initdeclarator => declarator initializer?

initializer => '=' constant

typespecifier => TYPESPEC
## typespecifier => structspecifier
typespecifier => typedefname

typedefname => identifier


declarator => pointr* directdeclarator

pointr => '*' TYPEQUALIFIER*

directdeclarator => identifier followdeclarator*

followdeclarator => arraydeclarator
followdeclarator => functiondeclarator

arraydeclarator => '[' expression ']'
arraydeclarator => '[' '*'? ']'


funcdeclaration => declspecifier identifier functiondeclarator

funcdefinition => declspecifier identifier functiondeclarator compoundstatement

functiondeclarator => '(' paramdeclaration,* ellipsis? ')'
    ##  Requires parameters in function declaration to include name, as well as type.

## functiondeclarator => '(' paramdecl,* ellipsis? ')'   # for params without names
## paramdecl => declspecifier+ abstractdeclarator
    # Need abstractdeclarator for parameters without name in function declarations

paramdeclaration => declspecifier declarator

ellipsis => ','  '...'


compoundstatement => '{' blockitem* '}'
blockitem => declaration ';'
blockitem => statement

declaration =>  declspecifier initdeclarator,+    # local declaration inside function

statement => if
statement => while
statement => do ';'
statement => for
statement => continue ';'
statement => break ';'
statement => compoundstatement
statement => ';'
statement => return ';'
statement => expression ';'

if => 'if' '(' test ')' suite else?
else => 'else' suite

while => 'while' '(' test ')' suite

suite => statement      # for compatibility with base.defn

do => 'do' statement 'while' '(' expression ')'

for => 'for' '(' expression? ';' expression? ';' expression? ')' statement
for => 'for' '(' declaration expression? ';' expression? ')' statement

continue => 'continue'
break => 'break'

return => 'return'
return => 'return' expression

expression => assignexpr
expression => test      # test specified in expr.syntax

assignexpr => lvalue '=' expression
assignexpr => lvalue ASSIGN_OP expression

lvalue => variable

factor => NOT_OP atom

atom => INC_OP variable 
atom => variable INC_OP

