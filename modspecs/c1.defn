# c1.defn

use base


typedef(typespecifier declarator)
    'type' declarator '=' typespecifier


globalvardeclaration(declspecifier identifier initializer?)
    'var' declspecifier identifier initializer?

initializer(constant)
    '=' constant

declspecifier(STORAGESPEC? TYPEQUALIFIER* typespecifier)
    STORAGESPEC? TYPEQUALIFIER* typespecifier


## specified only to concat words on one line
funcdeclaration(declspecifier identifier functiondeclarator)
    declspecifier identifier functiondeclarator

funcdefinition(typespecifier identifier functiondeclarator compoundstatement)
    'func' typespecifier identifier functiondeclarator '{'
    .indent()
    &compoundstatement
    .dedent()
    '}'

functiondeclarator(paramdeclaration* ellipsis?)
    '(' paramdeclaration* ellipsis? ')'

ellipsis()
    ',' '...'


# local declaration inside a function
declaration(typespecifier identifier initializer?)
    'var' typespecifier identifier initializer?


statement(expression)
    &expression
    'pop'               # discard expression value


for(declaration expression? expression? statement)
    &declaration
    =for(expression? expression? statement)

for(expression? expression? expression? statement)
    &expression?    # initialization, typically an assignment
    'pop'           # discard expression value
    =for(expression? expression? statement)

for(expression? expression? statement)
    for:
        &expression?            # condition
        'br' forbegin, forend
    forbegin:
        &statement
        &expression?            # next
        'pop'                   # discard expression value
        'br' for
    forend:

    
assignexpr(lvalue expression)
    &expression
    'dup'               # store consumes value, leave a copy
    'store' lvalue


atom(INC_OP variable)   # increment/decrement before use
    'load' variable
    'const 1'
    INC_OP                      # add or subtract
    'dup'                       # leave a copy on stack
    'store' .again(variable)    # reuse variable

atom(variable INC_OP)   # increment/decrement after use
    'load' variable
    'dup'                       # leave a copy on stack
    'const 1'
    INC_OP                      # add or subtract
    'store' .again(variable)    # reuse variable
    
INC_OP('++')
    'add'

INC_OP('--')
    'sub'


TYPESPEC('int')
    'i32'

TYPESPEC('char')
    'i8'

